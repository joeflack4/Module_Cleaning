********************************************************************************
*
*  FILENAME:	CCRX_FRQ_GGR_DataCleaning_v##_$date_initials.do
*  PURPOSE:		Label and encode questions in Global Gag Rule study
*  CREATED:		Andrea Rowan - IC (arowan44@gmail.com)
*  DATA IN:		CCRX_Female_Questionnaire_v#_results.csv
*  DATA OUT:	N/A
*  UPDATES:		2018-06-01 by Suzanne Bell (v3)
*					Added rename, label, and destring code for remaining variables
*					Made code generic with country specific secti ons
*
********************************************************************************

********************************************************************************
* RENAME VARIABLES
********************************************************************************

* Rename groups 
rename Afriend* friend*
rename Bwmn* nsum*
rename Bmn* nsum*
rename wmn* nsum*
rename ABTabt* abt*
rename ABTperiod* period*
rename self_unwanted_preg_yn self_unwanted_yn
rename nsum_modern_fp_followup nsum_fp_followup
rename self_abt_wmn_discussed self_abt_nsum_discussed
rename FVW* *
rename fp_visit_wait_month fp_visit_wait_min
rename fp_visit_wait_h fp_visit_wait_hour
rename abt_y_lab abt_year

********************************************************************************
* LABEL VARIABLES
********************************************************************************

* Label variables 
label var random_num "Random number generated by ODK for randomization"

label var friends				"Number of close female friends"
label var friend1_age 			"Age of female friend 1"
label var friend1_education 	"Highest level of school attended by friend 1"
label var friend2_age 			"Age of female friend 2"
label var friend2_education 	"Highest level of school attended by friend 2"
label var friend3_age 			"Age of female friend 3"
label var friend3_education 	"Highest level of education attended by friend 3"
label var friend1_fp			"Friend1: Uses IUD or implant"
label var friend1_abt_yn		"Friend1: Did something to end pregnancy"
label var friend1_abt_yr 		"Friend1: Year of last intentional ending of pregnancy"
label var friend1_abt_when 		"Friend1: Approx. date of last ending of pregnancy"
label var friend1_abt_comp		"Friend1: Had health issues after ending pregnancy and sought care at facility"
label var friend2_fp			"Friend2: Uses IUD or implant"
label var friend2_abt_yn		"Friend2: Did something to end pregnancy"
label var friend2_abt_yr 		"Friend2: Year of last intentional ending of pregnancy"
label var friend2_abt_when 		"Friend2: Approx. date of last intentional ending of pregnancy"
label var friend2_abt_comp		"Friend2: Had health issues after ending pregnancy and sought care at facility"
label var friend3_fp			"Friend3: Uses IUD or implant"
label var friend3_abt_yn		"Friend3: Did something to end pregnancy"
label var friend3_abt_yr 		"Friend3: Year of last intentional ending of pregnancy"
label var friend3_abt_when 		"Friend3: Approx. date of last intentional ending of pregnancy"
label var friend3_abt_comp		"Friend3: Had health issues after ending pregnancy and sought care at facility"

label var nsum_prompt			"Understands of contacts exercise"
label var nsum_modern_fp		"# of female contacts in last 12mo currently using IUD or implant"
label var nsum_abt				"# of female contacts in last 12mo who have ever intentionally ended a pregnancy"
label var nsum_abt_12mo			"# of female contacts in last 12mo who have intentionally ended a pregnancy in last 12mo"

label var self_unwanted_yn				"Self: Ever unwanted pregnancy"
label var self_abt_yn					"Self: Did something to stop unwanted pregnancy"

label var self_failed_fp_yn				"Self: Self or partner using FP method at time of unwanted pregnancy"
label var self_failed_fp_methods		"Self: What FP methods were used at time of unwanted pregnancy"
label var abt_month 					"Self: Month of abortion"
label var abt_year 						"Self: Year of abortion"
label var self_abt_where				"Self: Location of ending of pregnancy"
label var self_abt_comp					"Self: Experienced any health issues during ending of pregnancy and went to facility"
label var self_abt_friends_discussed	"Self: Discussed ending pregnancy with female contacts"
label var self_fp_friends_discussed		"Self: Discussed FP use with female contacts"
label var self_abt_nsum_discussed		"Self: # of female contacts who know about intentionally ended pregnancy"
label var nsum_fp_followup				"Self: # of female contacts who know about current FP method use"

********************************************************************************
* DESTRING/ENCODE VARIABLES
********************************************************************************

* Destring variables 
foreach var of varlist friends friend1_age friend2_age friend3_age nsum_modern_fp	///
nsum_abt nsum_abt_12mo self_abt_nsum_discussed nsum_fp_followup fp_visit_wait_min	///
fp_visit_wait_hour fp_visit_count abt_month abt_year {
capture destring `var', replace
}

* Label yes/no/prob variables
capture label define yes_prob_no_dk_list 0 no 1 yes 2 prob -88 "-88" -99 "-99" 

foreach var of varlist friend1_fp friend1_abt_yn friend1_abt_comp friend2_fp ///
friend2_abt_yn friend2_abt_comp friend3_fp friend3_abt_yn friend3_abt_comp self_abt_comp {
encode `var', gen(`var'v2) lab(yes_prob_no_dk_list)
}
label define yes_prob_no_dk_list 0 "No" 1 "Yes, I'm certain" 2 "Yes, I think so" ///
 -88 "-88" -99 "-99", replace

* Label abortion timing variables
capture label define abt_when2_list 1 0_to_1 2 1_to_3 3 3_to_5 4 5_plus ///
-88 "-88" -99 "-99" 

capture foreach var of varlist friend1_abt_when friend2_abt_when friend3_abt_when  {
encode `var', gen(`var'v2) lab(abt_when2_list)
}
capture label define abt_when2_list 1 "<1 year ago" 2 "1 to <3 years ago" ///
3 "3 to <5 years ago" 4 "5 or more years ago" -88 "-88" -99 "-99" , replace

* Label yes/no/dk variables 
capture label define yes_no_dnk_nr_list 0 no 1 yes -88 "-88" -99 "-99" 

foreach var of varlist nsum_prompt self_unwanted_yn self_failed_fp_yn {
encode `var', gen(`var'v2) lab(yes_no_dnk_nr_list)
}

* Label abortion attempt variable 
capture label define abt_attempt_list 0 no 1 yes 2 tried -88 "-88" -99 "-99" 

foreach var of varlist self_abt_yn {
encode `var', gen(`var'v2) lab(abt_attempt_list)
}
label define abt_attempt_list 0 "No" 1 "Yes, succeeded" 2 "Yes, but failed" ///
-88 "-88" -99 "-99", replace

* Generate binary variables for select-multiple FP question
gen self_fail_mthd_fem_steril=0 if self_failed_fp_methods!="" & self_failed_fp_methods!="-88" & self_failed_fp_methods!="-99" 
replace self_fail_mthd_fem_steril=1 if strpos(self_failed_fp_methods,"female_sterilization")

gen self_fail_mthd_male_steril=0 if self_failed_fp_methods!="" & self_failed_fp_methods!="-88" & self_failed_fp_methods!="-99" 
replace self_fail_mthd_male_steril=1 if strpos(self_failed_fp_methods,"male_sterilization")

split self_failed_fp_methods, gen(self_fail_mthd_opt_)
local x=r(nvars)
foreach var in implants IUD injectables pill emergency male_condoms ///
female_condoms diaphragm foam beads LAM rhythm withdrawal {
gen self_fail_mthd_`var'=0 if self_failed_fp_methods!="" & self_failed_fp_methods!="-88" & self_failed_fp_methods!="-99" 
forval y=1/`x' {
replace self_fail_mthd_`var'=1 if self_fail_mthd_opt_`y'=="`var'"
}
}
capture drop self_fail_mthd_opt_*
label define yn_list 0 "No" 1 "Yes"
label val self_fail_mthd_* yn_list

label var self_fail_mthd_fem_steril		"Self: Using female sterilization at time of unwanted preg"
label var self_fail_mthd_male_steril	"Self: Using male sterilization at time of unwanted preg"
label var self_fail_mthd_implants		"Self: Using implants at time of unwanted preg"
label var self_fail_mthd_IUD			"Self: Using IUD at time of unwanted preg"
label var self_fail_mthd_injectables	"Self: Using injectables at time of unwanted preg"
label var self_fail_mthd_pill			"Self: Using pill at time of unwanted preg"
label var self_fail_mthd_emergency		"Self: Using EC at time of unwanted preg"
label var self_fail_mthd_male_condoms	"Self: Using male condoms at time of unwanted preg"
label var self_fail_mthd_female_condoms	"Self: Using female condoms at time of unwanted preg"
label var self_fail_mthd_diaphragm		"Self: Using diaphragm at time of unwanted preg"
label var self_fail_mthd_foam			"Self: Using foam at time of unwanted preg"
label var self_fail_mthd_beads			"Self: Using beads at time of unwanted preg"
label var self_fail_mthd_LAM			"Self: Using LAM at time of unwanted preg"
label var self_fail_mthd_rhythm			"Self: Using rhythm method at time of unwanted preg"
label var self_fail_mthd_withdrawal		"Self: Using withdrawal at time of unwanted preg"

* Generate binary variables for self discussed abortion with friends question
split self_abt_friends_discussed, gen(self_abt_discuss_opt_)
local x=r(nvars)
foreach var in friend1 friend2 friend3 none {
gen self_abt_discuss_`var'=0 if self_abt_friends_discussed!="" & self_abt_friends_discussed!="-88" & self_abt_friends_discussed!="-99" 
forval y=1/`x' {
replace self_abt_discuss_`var'=1 if self_abt_discuss_opt_`y'=="`var'"
}
}
capture drop self_abt_discuss_opt_*
label val self_abt_discuss_* yn_list

label var self_abt_discuss_friend1		"Self: Discussed abortion with Friend 1"
label var self_abt_discuss_friend2		"Self: Discussed abortion with Friend 2"
label var self_abt_discuss_friend3		"Self: Discussed abortion with Friend 3"
label var self_abt_discuss_none			"Self: Did not discuss abortion with friends"

* Generate binary variables for self discussed FP methods with friends question 
split self_fp_friends_discussed, gen(self_fp_discuss_opt_)
local x=r(nvars)
foreach var in friend1 friend2 friend3 none {
gen self_fp_discuss_`var'=0 if self_fp_friends_discussed!="" & self_fp_friends_discussed!="-88" & self_fp_friends_discussed!="-99" 
forval y=1/`x' {
replace self_fp_discuss_`var'=1 if self_fp_discuss_opt_`y'=="`var'"
}
}
capture drop self_fp_discuss_opt_*
label val self_fp_discuss_* yn_list

label var self_fp_discuss_friend1		"Self: Discussed FP method with Friend 1"
label var self_fp_discuss_friend2		"Self: Discussed FP method with Friend 2"
label var self_fp_discuss_friend3		"Self: Discussed FP method with Friend 3"
label var self_fp_discuss_none			"Self: Did not discuss FP method with friends"

* Replace original variables with v2
unab vars: *v2
local stubs: subinstr local vars "v2" "", all
foreach var in `stubs'{
rename `var' `var'QZ
order `var'v2, after(`var'QZ)
}
rename *v2 *
drop *QZ

********************************************************************************
* COUNTRY SPECIFIC CLEANING
********************************************************************************

* Uganda country specific code
if "$CCRX"=="UGR6"  {

* Label variables
label var friend1_region 		"Region of residence of friend 1"
label var friend2_region 		"Region of residence of friend 2"
label var friend3_region 		"Region of residence of friend 3"
label var friend1_district		"District of residence of friend 1"
label var friend2_district		"District of residence of friend 2"
label var friend3_district		"District of residence of friend 3"

label var nsum_rec_birth		"# of female contacts in last 12mo gave birth in last 12mo"
label var nsum_twins			"# of female contacts in last 12mo gave birth to multiples in last 12mo"
label var nsum_cowife			"# of female contacts in last 12mo who have had least one co-wife"
label var nsum_edu				"# of female contacts in last 12mo with education past Senior 6"
label var nsum_smoke			"# of female contacts in last 12mo who smoke"
label var nsum_thatchedrf		"# of female contacts in last 12mo in househould with thatched roof"
label var nsum_car				"# of female contacts in last 12mo in household owning car/truck"
label var nsum_fridge			"# of female contacts in last 12mo in household with fridge"
label var nsum_exotic_cow		"# of female contacts in last 12mo in household owning exotic cow"
label var nsum_sheep			"# of female contacts in last 12mo in household owning >=1 sheep"
label var nsum_phone			"# of female contacts in last 12mo in household with landline"
label var nsum_piped			"# of female contacts in last 12mo in household with piped water inside"

* Destring variables
foreach var in nsum_rec_birth nsum_twins nsum_cowife nsum_edu nsum_smoke nsum_thatchedrf	///
nsum_car nsum_fridge nsum_exotic_cow nsum_sheep nsum_phone nsum_piped {
destring `var', replace
}

* Encode region geographic variable 
capture label define region_list 1 central 2 eastern 3 northern 4 western ///
-88 "-88" -99 "-99"

foreach var of varlist friend1_region friend2_region friend3_region {
capture encode `var', gen(`var'v2) lab(region_list)
}

label define region_list 1 "Central" 2 "Eastern" 3 "Northern" 4 "Western" ///
-88 "-88" -99 "-99", replace

* Encode district geographic variable?
/*
foreach var of varlist friend1_district friend2_district friend3_district {
encode `var', gen(`var'v2)
}
*/

* Label education variables 
capture label define school_list 1 never 2 primary 3 o 4 a 5 tertiary ////
6 university -88 "-88" -99 "-99"

foreach var of varlist friend1_education friend2_education friend3_education {
encode `var', gen(`var'v2) lab(school_list)
}

label define school_list 1 "Never attended" 2 "Primary" 3 "'O' level" ///
4 "'A' level" 5 "Tertiary" 6 "University" -88 "-88" -99 "-99", replace

* Label abortion location variables
capture label define providers_abt_list 1 govt_hosp 2 govt_health_center ///
3 public_FP 4 public_outreach 5 public_fieldworker 6 public_other ///
7 private_hosp 8 pharmacy 9 private_doc 10 private_outreach 11 private_fieldworker ///
12 private_other 13 shop 14 church 15 friend_relative 16 market 17 trad ///
18 self 19 other -88 "-88" -99 "-99" 

foreach var of varlist self_abt_where {
encode `var', gen(`var'v2) lab(providers_abt_list)
}
label define providers_abt_list 1 "Govt. Hospital" 2 "Govt. Health Center" ///
3 "Public FP Clinic" 4 "Public Outreach" 5 "Public Fieldwork/VHT" 6 "Other Public" ///
7 "Private Hospital/Clinic" 8 "Pharmacy" 9 "Private Doctor" 10 "Private Outreach" ///
11 "Private Fieldwork/VHT" 12 "Other Private" 13 "Shop" 14 "Church" 15 "Friend/relative" ///
16 "Market" 17 "Traditional Healer" 18 "Self" 19 "Other" -88 "-88" -99 "-99", replace 

}

else if "$CCRX"="ETR6" {

* Rename variables
rename nsum_cowife nsum_cleaner
rename nsum_edu nsum_teacher
rename nsum_poped_water nsum_piped

* Label variables
label var friend1_level1 		"Region of residence of friend 1"
label var friend2_level1 		"Region of residence of friend 2"
label var friend3_level1 		"Region of residence of friend 3"
label var friend1_level2		"Zone of residence of friend 1"
label var friend2_level2		"Zone of residence of friend 2"
label var friend3_level2		"Zone of residence of friend 3"
label var friend1_level3		"Woreda of residence of friend 1"
label var friend2_level3		"Woreda of residence of friend 2"
label var friend3_level3		"Woreda of residence of friend 3"
label var friend1_level3_other	"Other specify: Woreda of residence of friend 1"
label var friend2_level3_other	"Other specify: Woreda of residence of friend 2"
label var friend3_level3_other	"Other specify: Woreda of residence of friend 3"

label var nsum_rec_birth		"# of female contacts in last 12mo gave birth in last 12mo"
label var nsum_twins			"# of female contacts in last 12mo gave birth to multiples in last 12mo"
label var nsum_cleaner			"# of female contacts in last 12mo who work as a domestic helper/cleaner"
label var nsum_teacher			"# of female contacts in last 12mo who work as a teacher"
label var nsum_smoke			"# of female contacts in last 12mo who smoke cigarettes or a pipe"
label var nsum_secondary		"# of female contacts in last 12mo who have any education higher than secondary school"
label var nsum_piped			"# of female contacts in last 12mo in household with piped water inside"
label var nsum_comp				"# of female contacts in last 12mo in household that has a computer"
label var nsum_cart				"# of female contacts in last 12mo in household that owns an animal drawn cart"
label var nsum_motorcycle		"# of female contacts in last 12mo in household that owns a motorcycle or scooter"
label var nsum_bicycle			"# of female contacts in last 12mo in household that owns a bicycle"
label var nsum_camel			"# of female contacts in last 12mo in household that owns at least one camel"

* Destring variables
foreach var in nsum_rec_birth nsum_twins nsum_cleaner nsum_teacher nsum_smoke nsum_secondary	///
nsum_piped nsum_comp nsum_cart nsum_motorcycle nsum_bicycle nsum_camel {
destring `var', replace
}

* Destring and label region geographic variable 
label define regionlist 1 Tigray 2 Afar 3 Amhara 4 Oromiya 5 Somali 6 BG 7 SNNP 12 Gambella 13 Harari 14 Addis 15 "Dire Dawa"
foreach var in friend1_level1 friend2_level1 friend3_level1 {
gen `var'v2=substr(`var', 2,2)
destring `var'v2, replace
lab val `var'v2 region_list
order `var'v2, before(`var')
drop `var'
rename `var'v2 `var'
}

* Label education variables 
capture label define school_list 1 never 2 primary 3 secondary 4 technical 5 higher -88 "-88" -99 "-99"

foreach var of varlist friend1_education friend2_education friend3_education {
encode `var', gen(`var'v2) lab(school_list)
}

label define school_list 1 "Never attended" 2 "Primary" 3 "Secondary" ///
4 "Technical" 5 "Higher" -88 "-88" -99 "-99", replace

* Label provider variables
label define providers_abt_list 11 govt_hosp 12 govt_health_center 14 govt_health_post ///
25 FP_clinic 31 private_hospital 33 pharmacy 42 shop 22 volunteer 43 friend_relative ///
20 ngo 44 mobile_clinic 45 self 96 other -88 "-88" -99 "-99" 
	
encode self_abt_where, gen(self_abt_wherev2) lab(providers_abt_list)
	
label define providers_abt_list 11 "Govt. Hospital" 12 "Govt. Health Center" 14 "Govt. Health Post" ///
25 "FP Clinic" 31 "Private Hospital" 33 "Pharmacy" 42 "Shop" 22 "Volunteer" 43 "Friend/relative" ///
20 "NGO" 44 "Mobile Clinic" 45 "Self" 96 "Other" -88 "-88" -99 "-99" , replace

}

********************************************************************************
* DROP VARIABLES
********************************************************************************

* Drop variables
drop Asect_confidantes period_abt_last period_abt_last_full_lab sect_abt abt_future_error	///
Bsect_network_scale_up abt_y

